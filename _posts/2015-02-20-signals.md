---
title: Signals
titleSmall: Developing Games in Elm
titleStrong: Signals
date: 2015-02-20
category: developing-games-in-elm
tags: project-iso elm
---

This is the second primer article in the series _"Developing Games in Elm"_ in which I discuss the two main fundamental differences between [elm](http://elm-lang.org) and more traditional programming languages. If you haven't read the [previous article yet I recommend you do that first](/developing-games-in-elm/functional-programming).

Elm is not just a functional language, it is a functional _reactive_ language. It gives your game the ability to react to signals from the outside world; to player inputs and time.

In game development one of the most important concepts is the _event_. An event represents a thing that happened during the cycle of the game loop, for example mouse movement. The various modules in the code can attach a listener to the 'mouse move' event so that when it is triggered the listener function is run to modify some object(s).

{% highlight javascript %}
events.listen('mousemove', function(x, y) {
    world.scroll(x, y);
});
{% endhighlight %}

This is by it's very nature a side effect, which as you'll remember from the previous article is a bad idea.

Instead we want to be able to react to events in a pure functional way. We can do this with elm's signals.


## Time

A signal is a value that changes over time. The simplest example is time itself. Let's say we have a signal of milliseconds that updates every second:

{% highlight haskell %}
import Time (every, second)
import Signal (Signal)

time : Signal Int
time =
    every second
{% endhighlight %}

Since the value of the signal only changes each second, if we sample it multiple times within the same second we will get the same value back. The value isn't fired to us in a listener when it changes, instead we sample it from the signal.

Now we need a way to use this signal, since we can't listen to it and change some other object directly we have to map it into the game's state and handle the value from there.

As is common, elm uses the `main` function as it's entry point. This function should return an element which can be rendered:

{% highlight haskell %}
import Text (fromString, leftAligned)
import Graphics.Element (Element)

main : Element
main =
    "Hello, World!"
        |> fromString
        |> leftAligned
{% endhighlight %}

{:.share-elm}
[view on share-elm.com](http://share-elm.com/sprout/54e696e4e4b09711f39c2b6f)

But that is just static content, if we want dynamic content we output a signal of elements:

{% highlight haskell %}
import Text (asText)
import Time (every, second)
import Signal (Signal, map)
import Graphics.Element (Element)

main : Signal Element
main =
    every second
        |> map asText
{% endhighlight %}

{:.share-elm}
[view on share-elm.com](http://share-elm.com/sprout/54e692d3e4b09711f39c2b6d)

The value of the signal is mapped to the `Text.asText` function which passes it as a string into an element.

But we don't typically want to just blindly get the [time since 1970](http://www.wikiwand.com/en/Unix_time){:.external}. When making a game you need to get the exact delta since the last frame to ensure you can scale your time based calculations appropriately. This scaling prevents your game logic running at erratic speeds if your fps varies.

This is nice and simple in in elm, here we just output the delta every frame at a target of 60 frames per second:

{% highlight haskell %}
import Text (asText)
import Time (fps)
import Signal (Signal, map)
import Graphics.Element (Element)

main : Signal Element
main =
    fps 60
        |> map asText
{% endhighlight %}

{:.share-elm}
[view on share-elm.com](http://share-elm.com/sprout/54e6972ee4b09711f39c2b71)

## Input

As well as time, we can get inputs from the player. Continuing with our mouse move example; instead of listening for mouse move events, with elm we map the `Mouse.position` to our game logic:

{% highlight haskell %}
import Text (asText)
import Mouse
import Signal (Signal, map)
import Graphics.Element (Element)

main : Signal Element
main =
    Mouse.position
        |> map asText
{% endhighlight %}

{:.share-elm}
[view on share-elm.com](http://share-elm.com/sprout/54e698a4e4b09711f39c2b78)

Few games only take one input, so we can merge a second or more signals together.

{% highlight haskell %}
Signal.merge : Signal a -> Signal a -> Signal a
Signal.mergeMany : List (Signal a) -> Signal a
{% endhighlight %}

If multiple signals change at the same time priority is given to the left most signal. In the following example if the mouse is moving at the same time as it is clicked the `position` signal is given priority over `isDown`:

{% highlight haskell %}
import Text (asText)
import Mouse
import Signal (Signal, map, merge)
import Graphics.Element (Element)

main : Signal Element
main =
    merge
         (Mouse.position |> map asText)
         (Mouse.isDown |> map asText)
{% endhighlight %}

{:.share-elm}
[view on share-elm.com](http://share-elm.com/sprout/54e66a03e4b09711f39c2b37)


## Foldp

Now we have access to various input signals we want to apply these to our game state over time. Elm has this wonderful concept of folding over the past.

{% highlight haskell %}
Signal.foldp : (a -> state -> state) -> state -> Signal a -> Signal state
{% endhighlight %}

This takes a function which transforms the state with the value of the input signal and then outputs a signal with the new value of the state.

In this example we get the maximum position the mouse has been moved to, i.e. how far to the bottom right of the window.

{% highlight haskell %}
import Text (asText)
import Mouse
import Signal (Signal, map, foldp)
import Graphics.Element (Element)

main : Signal Element
main =
    maxMousePos
        |> map asText

maxMousePos : Signal (Int, Int)
maxMousePos =
    let update (x,y) (mx,my) =
            ( if x > mx then x else mx
            , if y > my then y else my
            )
    in foldp update (0,0) Mouse.position
{% endhighlight %}

{:.share-elm}
[view on share-elm.com](http://share-elm.com/sprout/54e69d1fe4b09711f39c2b81)

## Next

So we've now looked into the two most important differences between traditional languages and elm. Functional programming provides a more descriptive way to write code, and signals allow your game to react to time and inputs.

The next article in this series will delve into architecting a space invaders clone. With multiple modules, inputs, graphics and aliens it will demonstrate a successful approach to creating games in elm.

While you wait, why not [follow me on twitter](https://twitter.com/gelatindesign){:.external} and [streak club](http://streak.club/u/jellyislovely){:.external} to hear updates about my own game's progress and other development bits and bobs.

<!--
## Invaders

Lets put all this funky stuff into an actual game. Space Invaders seems like a nice basic one.

{% highlight haskell %}

{% endhighlight %}

-->

<!--
{% highlight haskell %}
import Text (asText)
import Mouse
import Signal (Signal, map, merge)
import Graphics.Element (Element)

type alias Points = List (Int, Int)

draw : Signal Points
draw =
    foldp (\click points ->
        if click
            then Window.dimensions |> map ((::) points)
            else points
    ) [] Mouse.clicks

main : Signal Element
main =
    map asText draw
{% endhighlight %}

{% highlight haskell %}
import Mouse
import Signal
import Graphics.Collage (..)
import Graphics.Element (..)

main : Signal Element
main =
    Signal.map2 reactiveSquare size color

reactiveSquare : Int -> Color.Color -> Element
reactiveSquare size color =
    square size |> filled color

size : Signal Int
size =
    Signal.map (\(x,y) -> x) Mouse.position

colour : Signal Color.Color
colour =
    Signal.map (\(x,y) ->
        if | y < 100 -> Color.red
           | y < 200 -> Color.green
           | otherwise -> Color.blue
    )

{% endhighlight %}
-->